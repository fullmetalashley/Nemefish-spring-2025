title: FirstCustomerGetContract_Dialogue
---
//Triggering dialogue is handled externally. Yarn nodes assume they begin immediately after the object click or event triggers it via Unity.
//start "FirstCustomerGetContract_Dialogue" after mousedown on customer.
// <<pose FirstCustomerTalking>>
// <<pose FOHISittingNeutral>>
// <<pose PCStandingNeutral>>
<<set $FirstCustomerVisited = true>>
    Customer: Well, hey there! You with Nixers Inc.?
    -> POKE SOME FUN. 
        Expecting anyone else? It sure is empty out here.
        <<jump EmptyOutHere>>
    -> BE POLITE.
        At your service, sir!
        <<jump Polite>>
===

title: EmptyOutHere
---
Customer: I suppose you're right, though you should know it wasn't always so deserted. Or quite so wet, if we're being specific. This your first time visiting Swallowed Strait?
-> POKE SOME FUN.
        Yes, sirree. If you'll forgive my saying so, most folks don't just stumble into this neck of the woods.
        <<jump AnySaySo>>
-> BE POLITE.
        Sure is! I'm looking forward to separating fact from fiction.
        <<jump AnySaySo>>
===

title: Polite
---
Customer: Sir! Haha <i>sir</i>! Please, friend. I'm just a customer. But, your accent— you're Inlander, right?
You: Born and raised.
Customer: Well, Inlander, you'll find people out here aren't so formal. We're a scruffy bunch. Scruffy but kind-hearted. Must be something about nature and nurture. First time visiting the Swallowed Strait?
-> POKE SOME FUN.
        Yes, sirree. If you'll forgive my saying so, most folks don't just stumble into this neck of the woods.
        <<jump AnySaySo>>
-> BE POLITE.
        Sure is! I'm looking forward to separating fact from fiction.
        <<jump AnySaySo>>
===

title: AnySaySo
---
// <<pose FirstCustomerOther>>
Customer: You'd surprised how many people love this Strait—myself included. I'd even say it's the prettiest spot on the Drowned Coast. In fact, that's why I contacted the Nixers.

See that blotch on the horizon? That's Dismal Notch Island. I grew up there. It's got a little town right on the waterfront. A very nice boardwalk, too. I used to run down it back when I was my son's age. I'd a funny superstition about landing just right on the boards...  
-> DIG DEEPER.
    The old "Don't step on a crack or you'll break your mother's back?"
    <<jump BreakBack>>
-> MOVE THINGS ALONG.
    So, what do you need me for?
    <<jump WhatNeedMe>>
===

title: BreakBack
---
// <<pose FirstCustomerTalking>>
Customer: The very one! So, they must say the same thing Inland. What an odd thing to teach kids, huh? I mean, why make them feel it was up to them—who lived and died? I'm sorry, excuse me. What use is there in combing over the past these days?
-> POKE SOME FUN.
    Hey, one man's past is another man's presents. So, how can I help?
    <<jump WhatNeedMe>>
-> BE POLITE.
    No need to apologize. So, how can I help?
    <<jump WhatNeedMe>>
===

title: WhatNeedMe
---
Customer: I'm sure you've heard things can be... strange out here. I was preparing to move back to the island, see, with my family. Got so far as sending word to my Aunt who lives on the—well, that doesn't matter. Anyway, she warned me that there was some kind of <i>nemefish</i> living offshore from the town. 

Now, if it was just me, I'd risk it. But I've got two kids, a wife... Without her, who'd tell me I had crumbs in my beard, you know?
-> DIG DEEPER.
    Can you give me any more information?
    <<jump MoreInfo>>
===

title: MoreInfo
---
Customer: I wish. I imagine Nixers told you that you're not the first to come. There was another Exterminator a month ago. Never came back. If she had, maybe I'd know more.
-> POKE SOME FUN.
    Well, I don't know anyone who's picked my line of work for the pension.
-> BE POLITE.
    We all know the risks.
// <<pose FirstCustomerCrying>>
Customer: Perhaps. But it's never just the one person who's lost, is it? 
-> BE POLITE.
    You're kind to consider my safety, but I'm up for this job.
    <<jump UpForJob>>
-> MOVE THINGS ALONG.
    Don't worry. I'm up for this job.
    <<jump UpForJob>>
===

title: UpForJob
---
// <<pose FirstCustomerTalking>>
Customer: I'm sure you are. But I <i>do</i> feel responsible, you know. For what happens to you. For even calling Nixers. And your... cat? What does he do?
Theodore: Meow.
-> POKE SOME FUN.
    This fleabag? He's my insurance policy in case I need time to escape!
    <<jump TossToNemefish>>
-> BE POLITE.
    He might be cute, but Theodore here isn't your ordinary cat. He's what we call a F.O.H.I.—a feline of human intelligence. He's more vital to me than my fishing rod.
    <<jump VitalToMe>>
===

title: TossToNemefish
---
<<set $Relate2FOHI = $Relate2FOHI - 2>>
<<ShowNotification "Theodore didn't appreciate that.">>
//UI script commandhandler cue
// <<pose FOHISittingHissing>>
Theodore: Hiss.
// <<pose FOISittingNeutral>>
Customer: I'd mind what you say—he doesn't seem too forgiving! Well, here's the contract.
<<set $hasContract = true>>
<<showNotification "You've received your contract.">>
//UI script commandhandler cue
<<inventoryAdd "Contract">>
-> MOVE THINGS ALONG.
    And my payment? We take half up-front.
    <<jump HalfUpFront>>
===

title: VitalToMe
---
<<set $Relate2FOHI = $Relate2FOHI + 2>>
<<ShowNotification "Theodore really appreciated that.">>
//UI script commandhandler cue
Theodore: Meow.
Customer: I suppose you're the expert. Well, here's the contract.
<<showNotification "You've received your contract.">>
//UI script commandhandler cue
<<inventoryAdd "Contract">>
-> MOVE THINGS ALONG.
    And my payment? We take half up-front.
    <<jump HalfUpFront>>
===

title: HalfUpFront
---
// <<pose FirstCustomerWorried>>
Customer: Oh! I gave it to your colleague—before she, erm,  well, before she vanished. Customer Service said you'd retrieve it. Something about a warranty? Should be in her pack. 
// <<pose FirstCustomerTalking>>
Now, if you don't mind, I'm soaked through. If—when you finish, you can find me here. I'll see you coming from my spot down the way. Good luck!
<<showNotification "Your Quest Log has been updated.">>
<<questUpdate "KillNemefish">>
<<questUpdate "FindPayment">>
//UI script commandhandler cue
<<disableNPC FirstCustomer>>
<<jump LetsGo>>
===

title: LetsGo
---
<<if $Relate2FOHI == -2>>
<<wait 1>>
You: A missing Exterminator, huh? Typical Dispatch not mentioning <i>that</i>. And no payment either. Boy, do we have our jobs cut out for us!
Theodore: Hiss.
You: Are you still upset about that silly joke?
Theodore: Meow. 
    -> POKE SOME FUN.
        How about we call it even for you sleeping on my head all night?
        Theodore: Meow.
        <<set $Relate2FOHI = $Relate2FOHI + 1>>
        <<showNotification "Theodore concedes your point.">>
        //UI script commandhandler cue
    -> BE POLITE.
        Oh, you should know by now not to take a blunt instrument like me so seriously. Still, I'm sorry I hurt your feelings.
        Theodore: <iMeow</i>.
        <<set $Relate2FOHI = $Relate2FOHI + 1>>
        <<showNotification "Theodore appreciates your apology.">>
        //UI script commandhandler cue
            -> MOVE THINGS ALONG.
                Now that's settled, let's take off! 

<<else>>
You: A missing Exterminator, huh? Typical Dispatch not mentioning <i>that</i>. And no payment either. Boy, do we have our jobs cut out for us!  
Theodore: Meow.
-> MOVE THINGS ALONG.
    Ready, Theodore?
Theodore: Meow.
-> MOVE THINGS ALONG.
    Then we're off. 
<<endif>>
===